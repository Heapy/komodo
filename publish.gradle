apply(plugin: "maven-publish")
apply(plugin: "java-library")
apply(plugin: "com.jfrog.bintray")
apply(plugin: "org.jetbrains.dokka")

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

dokka {
    outputFormat = "html"
    outputDirectory = javadoc.destinationDir
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    from javadoc.destinationDir
    classifier = "javadoc"
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/${project.name}-${project.version}.pom")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact dokkaJar

            pom {
                name = project.name
                description = project.description
                url = "https://heapy.io/komodo"

                licenses {
                    license {
                        name = "GNU General Public License v3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }

                developers {
                    developer {
                        id = "IRus"
                        name = "Ruslan Ibragimov"
                        email = "ruslan@ibragimov.by"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/Heapy/komodo.git"
                    developerConnection = "scm:git:git@github.com:Heapy/komodo.git"
                    url = "https://github.com/Heapy/komodo"
                }
            }

            from components.java
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    filesSpec {
        from 'build/libs'
        into '.'
    }

    pkg {
        userOrg = "heapy"
        repo = project.version.endsWith("dev") ? "heap" : "heap-dev"
        name = "komodo"
        websiteUrl = "https://heapy.io/komodo"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/Heapy/komodo"
        publications = ["maven"]
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}
